/*
 * Execute with following command to create executables: gradle build createExe dependencyUpdates -Drevision=release
 */

plugins {
    id 'java'
    id 'edu.sc.seis.launch4j' version '2.4.6'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'application'
    id "edu.sc.seis.macAppBundle" version "2.3.0"
    id "com.github.ben-manes.versions" version "0.21.0"
}


repositories {
    mavenLocal()
    maven {
        url = 'https://repository.jboss.org/nexus/content/repositories/thirdparty-releases/'
    }

    maven {
        url = 'http://central.maven.org/maven2/'
    }

    maven {
        url = 'https://jitpack.io'
    }

    maven {
        url = 'http://repo.maven.apache.org/maven2'
    }
}

dependencies {
    compile 'org.slf4j:slf4j-simple:2.0.0-alpha0'
    compile 'javax.activation:activation:1.1.1'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'org.apache.pdfbox:pdfbox:2.0.16'
    compile 'com.github.Arnauld:gutenberg:18d761ddba'
    compile 'org.scilab.forge:jlatexmath:1.0.7'
    compile 'com.itextpdf:itextpdf:5.5.13.1'
    compile 'com.itextpdf.tool:xmlworker:5.5.13.1'
    compile files("${projectDir}/lib/jai_core-1.1.3.jar")
    compile files("${projectDir}/lib/mathocr-0.0.3.jar")
    compile 'io.github.soc:directories:11'
    compile 'com.bulenkov:darcula:2018.2'
}
application {
    mainClassName = "${project.mainClassName}"
}

run {
    args = ['-NSRequiresAquaSystemAppearance','False']
}

group = 'com.github.moritzfl'
version = '2.2.2.1'
description = 'mathematicallatexhelper'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
mainClassName = 'de.moritzf.latexhelper.MathematicalLatexHelperGui'

launch4j {
    mainClassName = "${project.mainClassName}"
    icon = "${projectDir}/src/main/resources/icon.ico"
    jar = "${projectDir}/build/libs/${project.tasks.shadowJar.archiveName}"
}

manifest {
    attributes 'Main-Class': mainClassName
}

tasks.createExe.dependsOn('shadowJar')

macAppBundle {
    version = "${version}"
    mainClassName = "${project.mainClassName}"
    bundleExecutable = "${project.description}"
    bundleIdentifier = "${project.group}.${project.description}"
    icon = "${projectDir}/src/main/resources/icon.icns"
    bundleJRE = false
    runtimeConfigurationName = 'shadow'
    jarTask = 'shadowJar'
    bundleExtras.put("NSRequiresAquaSystemAppearance", "NO")
}
